sourceSets {
    integTest {
        java.srcDir 'src/integTest/java'
        resources.srcDir 'src/integTest/resources'
    }
}

dependencies {
    /* provide integration tests with the output of the main/test sourceSet as compile-time dependencies */
    integTestCompile sourceSets.main.output
    integTestCompile sourceSets.test.output

    /* provide integration tests with the same compile-time dependencies as the main & test sourceSets */
    integTestCompile configurations.compile
    integTestCompile configurations.testCompile

    /* provide integration tests with the same run-time dependencies as the main & test sourceSets */
    integTestRuntime configurations.runtime
    integTestRuntime configurations.testRuntime
}

task integTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs the integration tests.'

    maxHeapSize = '1024m'

    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath

    binResultsDir = file("$buildDir/integration-test-results/binary/integTest")

    reports {
        html.destination = "$buildDir/reports/integration-test"
        junitXml.destination = "$buildDir/integration-test-results"
    }

    mustRunAfter tasks.test
}

/* add the integTest to the check lifecycle */
check.dependsOn integTest

/* fail fast by ensuring integration tests only run after the verification tasks that are quick to execute */
gradle.projectsEvaluated {
    def quickTasks = []

    gradle.rootProject.allprojects.each { project ->
        quickTasks.addAll(project.tasks.findAll { it.name == 'test' })
        quickTasks.addAll(project.tasks.withType(FindBugs))
        quickTasks.addAll(project.tasks.withType(Pmd))
    }

    quickTasks.each { task ->
        project.tasks.integTest.mustRunAfter task
    }
}
