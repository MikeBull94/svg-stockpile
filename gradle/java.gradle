apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
    doclet {
        description = 'Configuration for doclet artifacts.'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    doclet deps.apiviz
    compile deps.guava
    compile deps.slf4j
    testCompile deps.junit
    testCompile deps.mockito
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

tasks.javadoc {
    doFirst {
        sourceSets.main.output.each { dir ->
            dir.mkdirs()
        }
    }

    title = "$rootProject.name/$project.name $project.version"

    options {
        doclet = 'org.jboss.apiviz.APIviz'
        docletpath = configurations.doclet.files.asType(List)

        addBooleanOption('nopackagediagram', true)
        addStringOption('sourceclasspath', files(sourceSets.main.output).asPath)

        showAll()
        links 'https://docs.oracle.com/javase/8/docs/api/'
        links 'http://www.slf4j.org/api/'
        links "https://google.github.io/guava/releases/$deps.guava.version/api/docs/"
    }
}

task javadocJar(type: Jar, dependsOn: tasks.javadoc) {
    group = LifecycleBasePlugin.BUILD_GROUP
    description = 'Assembles a jar archive containing the Javadoc API documentation.'
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    group = LifecycleBasePlugin.BUILD_GROUP
    description = 'Assembles a jar archive containing the main classes with sources.'
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
