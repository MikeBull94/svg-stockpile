import org.apache.tools.ant.filters.ReplaceTokens

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/integTest.gradle"
apply from: "$rootDir/gradle/findbugs.gradle"
apply from: "$rootDir/gradle/pmd.gradle"
apply from: "$rootDir/gradle/publish.gradle"

description = 'Gradle Plugin that interacts with the svg-stockpile API.'

dependencies {
    compile gradleApi()
    compile project(':api')
    integTestCompile gradleTestKit()
    integTestCompile group: 'org.codehaus.groovy', name: 'groovy-all'
}

javadoc.options {
    links 'https://docs.gradle.org/current/javadoc/'
}

def integTestRepo = "file://$buildDir/integration-test-repo"

processIntegTestResources {
    filter(ReplaceTokens, tokens: [
        'version': version,
        'integTestRepo': integTestRepo.toString()
    ])
}

task uploadLocalApi(type: Upload) {
    group = BasePlugin.UPLOAD_GROUP
    description = "Uploads all artifacts belonging to ${project(':api').configurations.archives} to $integTestRepo"
    configuration = project(':api').configurations.archives

    repositories {
        mavenDeployer {
            repository(url: integTestRepo)
        }
    }
}

task uploadLocalGradlePlugin(type: Upload) {
    group = BasePlugin.UPLOAD_GROUP
    description = "Uploads all artifacts belonging to $configurations.archives to $integTestRepo"
    configuration = configurations.archives

    repositories {
        mavenDeployer {
            repository(url: integTestRepo)
        }
    }
}

integTest.dependsOn uploadLocalApi, uploadLocalGradlePlugin
