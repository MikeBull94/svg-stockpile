import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/integTest.gradle"
apply from: "$rootDir/gradle/findbugs.gradle"
apply from: "$rootDir/gradle/pmd.gradle"
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'

description = 'Gradle plugin that interacts with the svg-stockpile API.'

dependencies {
    compile gradleApi()
    compile project(':api')
    integTestCompile gradleTestKit()
    integTestCompile group: 'org.codehaus.groovy', name: 'groovy-all'
}

shadowJar {
    manifest {
        attributes 'Main-Class': 'com.mikebull94.stockpile.Example'
    }

    baseName = rootProject.name
    classifier = ''
}

publishing {
    publications {
        shadow(MavenPublication) {
            groupId rootProject.group
            artifactId rootProject.name
            version rootProject.version
            artifact shadowJar
        }
    }
}

processIntegTestResources {
    filter(ReplaceTokens, tokens: [
        'version': rootProject.version
    ])
}

integTest.dependsOn publishToMavenLocal
